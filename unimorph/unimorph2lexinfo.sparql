PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX olias: <http://purl.org/olia/system.owl#>
PREFIX olia: <http://purl.org/olia/olia.owl#>
PREFIX oliat: <http://purl.org/olia/olia-top.owl#>
PREFIX udata:  <https://unimorph.github.io/>
PREFIX umodel: <http://purl.org/olia/unimorph/unimorph.owl#>
PREFIX apf: <http://jena.apache.org/ARQ/property#>
PREFIX lexinfo2: <http://www.lexinfo.net/ontology/2.0/lexinfo#>
PREFIX lexinfo: <http://www.lexinfo.net/ontology/3.0/lexinfo#>

#
# bootstrapping mapping from unimorph labels to OLiA classes and lexinfo features
#################################################################################
# we expect OLiA to be loaded in default graph
## currently, it isn't loaded

#SELECT REDUCED ?umprop ?umlabel ?oliaclass ?lprop ?lobject
CONSTRUCT {
  [a ?oliaclass ] ?lprop ?lobject; ?umprop ?umlabel.
}
WHERE {

  # (loose graph name match to account for using local *or* remote files)

  # unimorph
  ###########
  # note that the Unimorph ontology doesn't use conventional olias:hasTag, etc.
  # instead, it assumes that tags are *broken* along ; and then matched against umodel:hasLabel

  GRAPH ?unimorph {
    ?um a/rdfs:subClassOf* ?umclass.
    ?um ?umprop ?umlabel.
    FILTER(?umprop in (umodel:hasLabel,umodel:hasLabelStartingWith,umodel:hasLabelMatching))
  }
  FILTER(strends(str(?unimorph),"unimorph.owl"))

  { GRAPH ?ulink {
      MINUS { ?umclass (rdfs:subClassOf|owl:equivalentClass|owl:intersectionOf|owl:joinOf) [] }
    } FILTER(strends(str(?ulink),"unimorph-link.rdf"))
  } UNION
  {
    # unimorph => OLiA
    ##################

    GRAPH ?ulink {
        ?umclass (rdfs:subClassOf|owl:equivalentClass|((owl:intersectionOf|owl:joinOf)/rdf:rest*/rdf:first))+ ?oliaclass.
        FILTER(contains(str(?oliaclass), "olia.owl"))
        FILTER(contains(str(?umclass), "unimorph"))
    }
    FILTER(strends(str(?ulink),"unimorph-link.rdf"))

      # OLiA => lexinfo
      #################
      # direct dependents, only
      # for indirect matches, look into ?olia (default graph)

      {
        GRAPH ?llink {
          MINUS { [] a|rdfs:subClassOf ?oliaclass }
        }
        FILTER(strends(str(?llink),"lexinfo-link.rdf"))
      } UNION {

        GRAPH ?llink {
          ?lobject a/rdfs:subClassOf* ?oliaclass
        }
        FILTER(strends(str(?llink),"lexinfo-link.rdf"))


        GRAPH ?lexinfo {
          ?lprop rdfs:range ?lfeat.
          ?lobject a ?lfeat.
        }
        FILTER(strends(str(?lexinfo),"lexinfo.owl"))
      }
    }
} ORDER BY ?umlabel
